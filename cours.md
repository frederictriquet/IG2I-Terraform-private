---
marp: true
theme: default
paginate: true
math: mathjax
---
<style>
section::after {
    content: attr(data-marpit-pagination) '/' attr(data-marpit-pagination-total)
}
.highlight-red {
    color: red;
    font-weight: bold;
}
.highlight-blue {
    color: blue;
    font-weight: bold;
}
.highlight-green {
    color: green;
    font-weight: bold;
}
</style>

# Cours d'introduction √† Terraform

IG2I - 2025 - Fr√©d√©ric Triquet

---

<style scoped>
.columns {
  display: grid;
  grid-template-columns: 1fr 1fr;
  gap: 2rem;
}
</style>

## Table des mati√®res

<div class="columns">
<div>

1. Introduction √† l'Infrastructure en tant que Code
2. Qu'est-ce que Terraform ?
3. Approches traditionnelles vs Terraform
4. Terraform vs Ansible

</div>
<div>

5. Concepts fondamentaux
6. Flux de travail Terraform
7. Concepts avanc√©s
8. Outils alternatifs

</div>
</div>

## D√©roulement de la journ√©e

  Cours puis TP, ce matin et cet apr√®s-midi

---

# 1. Introduction √† l'Infrastructure en tant que Code

---

## Qu'est-ce que l'IaC ?

L'Infrastructure en tant que Code est la pratique de gestion et d'approvisionnement de l'infrastructure via des fichiers lisibles par la machine.

<!--
du code = des fichiers suffisamment simples pour √™tre lus et √©crits par l'Homme / suffisamment structur√©s pour √™tre compris par la machine -->



### Avantages cl√©s de l'IaC

- **"gitable"**
  - Contr√¥le de version pour suivre les modifications
  - Collaboration
- **Reproductible**
  - Environnements identiques
  - **Automatisation** : R√©duction des erreurs humaines
- **Documentation** : Le code est source de documentation

---

## √âvolution : Configuration manuelle

**Ann√©es 1990-2000**
- Pas de cloud
- Pas de virtualisation
- Pas de conteneurs
- Les infrastructures sont g√©n√©ralement de taille modeste
- Configuration manuelle via SSH/RDP
- Documentation dans des wikis ‚Üí Risques de d√©rive et d'incoh√©rence

<!-- les outils apparaissent avec les besoins
au d√©but : infra simples

avant 2000 : il y a de la virtualisation mais pas pour faire de la prod

au fil des ans : augmentation de la puissance et baisse des co√ªts
 -->
---

## √âvolution : Gestion de configuration

**Ann√©es 2000-2010**
- Pas de cloud
- D√©but de la virtualisation
- Pas de conteneurs
- La taille des infrastructures augmente
- Outils : Puppet, Chef, Ansible
- Automatisation de la configuration
- Focus sur la configuration logicielle

<!--
d√©but de la virtu pour de la prod

-->
---

## √âvolution : Infrastructure as Code

**Ann√©es 2010-aujourd'hui**
- D√©but du cloud
- D√©but des conteneurs
- Explosion de la taille des infrastructures et de la diversit√© de leurs composants
- Outils : Terraform, CloudFormation
- Cycle de vie complet de l'infrastructure

---

# 2. Qu'est-ce que Terraform ?

---

## Vue d'ensemble

**Terraform** est un outil d'_Infrastructure as Code_ open-source cr√©√© par HashiCorp.

Il permet de d√©finir des ressources cloud et on-premise dans des fichiers de configuration lisibles.

---

## Caract√©ristiques cl√©s

- **Langage d√©claratif** : Description du r√©sultat final attendu
- **Agnostique du cloud** : Un seul outil pour AWS, Azure, GCP, etc.
- **Gestion d'√©tat** : L'√©tat actuel de l'infrastructure est stock√© dans une base de donn√©es
- **Planification** : Pr√©visualisation des modifications avant de les appliquer
- **Gestion des d√©pendances** : R√©solution automatique

üí° _Gestion d'√©tat, planification et d√©pendances seront vues en d√©tail durant les exercices_

---

# 3. Approches traditionnelles vs Terraform

---

## Approche 1 : Configuration manuelle

**Cr√©er une instance EC2 manuellement :**
1. Se connecter √† la console AWS
2. Naviguer vers EC2
3. Cliquer sur "Launch Instance"
4. Configurer AMI, type, stockage, tags
5. Lancer et sauvegarder la cl√©

---

## Probl√®mes de l'approche manuelle

- ‚ùå Fastidieux (lourdeur des portails des cloud providers)
- ‚ùå Risque d'oubli d'un param√®tre
- ‚ùå Difficile √† reproduire
- ‚ùå Chronophage pour plusieurs ressources
- ‚ùå Pas de contr√¥le de version
- ‚ùå Connaissances dans la t√™te des gens
- ‚ùå Pas de piste d'audit

<!-- ---

## Solution Terraform

```hcl
resource "aws_instance" "web" {
  ami           = "ami-0c55b159cbfafe1f0"
  instance_type = "t2.micro"

  tags = {
    Name = "WebServer"
  }
}
``` -->
<!-- 
---

## Avantages Terraform

- ‚úÖ Versionn√© dans Git
- ‚úÖ Une commande : `terraform apply`
- ‚úÖ Document√© dans le code
- ‚úÖ Scalable (1 ou 100 instances)
- ‚úÖ Outils qui analysent le code terraform
 -->
---

## Approche 2 : Scripts shell

```bash
#!/bin/bash
# Cr√©er VPC
VPC_ID=$(aws ec2 create-vpc --cidr-block 10.0.0.0/16 \
  --query 'Vpc.VpcId' --output text)

# Cr√©er Subnet
SUBNET_ID=$(aws ec2 create-subnet --vpc-id $VPC_ID \
  --cidr-block 10.0.1.0/24 --output text)

# Cr√©er instance
aws ec2 run-instances --image-id ami-xxx \
  --instance-type t2.micro --subnet-id $SUBNET_ID
```

---

## Probl√®mes des scripts shell

- ‚ùå Mod√®le **Imp√©ratif** : Il faut sp√©cifier chaque √©tape
- ‚ùå Pas de gestion des d√©pendances : **dans le bon ordre**
- ‚ùå Pas de gestion d'√©tat : Une r√©-ex√©cution cr√©e des doublons
- ‚ùå Gestion d'erreurs complexe : Mon script √©choue √† la ligne 700... üò±üò±üò±
- ‚ùå Pas de dry-run

---

## Solution Terraform (d√©clarative)

```hcl
resource "aws_vpc" "main" {
  cidr_block = "10.0.0.0/16"
}

resource "aws_subnet" "public" {
  vpc_id     = aws_vpc.main.id
  cidr_block = "10.0.1.0/24"
}

resource "aws_instance" "web" {
  ami           = "ami-0c55b159cbfafe1f0"
  instance_type = "t2.micro"
  subnet_id     = aws_subnet.public.id
}
```

---

## Avantages de Terraform

- ‚úÖ Mod√®le **D√©claratif** : d√©crire l'√©tat souhait√©
- ‚úÖ R√©solution automatique des d√©pendances : l'ordre n'importe pas
- ‚úÖ Gestion d'√©tat : il sait ce qui existe
- ‚úÖ Idempotent : on peut l'ex√©cuter plusieurs fois sans probl√®me
- ‚úÖ Mises √† jour et suppressions faciles
- ‚úÖ Versionn√© dans Git
- ‚úÖ Une commande : `terraform apply`
- ‚úÖ Document√© dans le code
- ‚úÖ Scalable (1 ou 100 instances)
- ‚úÖ Outils qui analysent le code terraform

---

## Approche 3 : Outils cloud-sp√©cifiques

**CloudFormation (AWS), ARM Templates (Azure)**

**Probl√®mes :**
- ‚ùå Verrouillage fournisseur
- ‚ùå Limit√© √† un seul cloud
- ‚ùå Syntaxe JSON/YAML complexe

---

# 4. Terraform vs Ansible

<!--
on parle souvent de ces 2 outils
on peut les confondre (quand on ne les conna√Æt pas bien)

points communs : IaC, HashiCorp
-->
---

## Diff√©rences cl√©s

|              | Ansible                   | Terraform                              |
| ------------ | ------------------------- | -------------------------------------- |
| **Objectif** | Configuration de serveurs | Provisionnement de ressources          |
| **Langage**  | YAML                      | HCL (Hashicorp Configuration Language) |
| **√âtat**     | Sans √©tat                 | Avec √©tat                              |
| **Approche** | Push (SSH)                | API-based                              |

‚ö†Ô∏è Il y a des actions r√©alisables avec les deux outils

---

## Exemple : Ansible

```yaml
- name: Provision EC2 instance
  hosts: localhost
  tasks:
    - name: Create EC2 instance
      ec2:
        key_name: mykey
        instance_type: t2.micro
        image: ami-0c55b159cbfafe1f0
        region: us-east-1
```

---

## Exemple : Terraform

```hcl
resource "aws_instance" "web" {
  ami           = "ami-0c55b159cbfafe1f0"
  instance_type = "t2.micro"
  key_name      = "mykey"

  tags = {
    Name = "WebServer"
  }
}

output "instance_ip" {
  value = aws_instance.web.public_ip
}
```

---

<style scoped>
.columns {
  display: grid;
  grid-template-columns: 1fr 1fr;
  gap: 0rem;
}
</style>

## Quand utiliser quoi ?

<div class="columns">
<div>

### Quand utiliser Terraform ?

- Cr√©ation d'infrastructure cloud
- Gestion du cycle de vie (cr√©er, modifier, d√©truire) de ressources cloud
- Modules d'infrastructure r√©utilisables

‚û°Ô∏è De quelle infrastructure ai-je besoin ?

</div>
<div>

### Quand utiliser Ansible ?

- Configuration de logiciels sur serveurs existants
- D√©ploiement d'applications
- Commandes ad-hoc
- Gestion de fichiers de configuration
- Orchestration multi-√©tapes

‚û°Ô∏è Comment configurer ces serveurs ?

</div>
</div>

---

## Utilisez les deux ensemble !

```
Terraform ‚Üí Cr√©er l'infrastructure
  ‚îú‚îÄ Instances EC2
  ‚îú‚îÄ RDS
  ‚îî‚îÄ Load balancer
     ‚Üì
Ansible ‚Üí Configurer l'infrastructure
  ‚îú‚îÄ Installer logiciels
  ‚îú‚îÄ Configurer applications
  ‚îî‚îÄ D√©ployer le code
```

---

## Workflow combin√©

```bash
# 1. Cr√©er l'infrastructure
terraform apply

# 2. Obtenir les IPs
terraform output -json > inventory.json

# 3. Configurer les serveurs
ansible-playbook -i inventory.json configure.yml
```

- ‚ö†Ô∏è les outputs doivent √™tre adapt√©s ‚ö†Ô∏è
- l'utilisation de l'inventaire automatique est quand m√™me pr√©f√©rable

---

# 5. Concepts fondamentaux

---

## Providers (Fournisseurs)

Les providers sont des plugins permettant √† Terraform d'interagir avec les APIs.

```hcl
provider "aws" {
  region = "us-east-1"
}

provider "google" {
  project = "my-gcp-project"
  region  = "us-central1"
}
```

---

## Providers populaires

- **Cloud** : AWS, Azure, Google Cloud
- **Conteneurs** : Kubernetes, Docker
- **DevOps** : GitHub, GitLab
- **Monitoring** : Datadog, PagerDuty
- **Et plus de 2000 autres !**

---

## Resources (Ressources)

Les ressources sont l'√©l√©ment central de Terraform.
Elles sont fournies par les providers.
1 ressource $\approx$ 1 √©l√©ment d'infrastructure
**Syntaxe :**

<pre>resource "<span class="highlight-red">TYPE_RESSOURCE</span>" "<span class="highlight-blue">NOM</span>" {
  argument1 = valeur1
  argument2 = valeur2
}</pre>

---

## Exemple de ressource

<pre>
resource "<span class="highlight-red">aws_instance</span>" "<span class="highlight-blue">web</span>" {
  ami           = "ami-0c55b159cbfafe1f0"
  instance_type = "t2.micro"

  tags = {
    Name = "MyWebServer"
  }
}

# R√©f√©rencer une autre ressource
resource "aws_eip" "web_ip" {
  instance = <span class="highlight-red">aws_instance</span>.<span class="highlight-blue">web</span>.id
}
</pre>

---

## Data Sources (Sources de donn√©es)

R√©cup√©rer et utiliser des informations sur des ressources existantes.

<pre>
<span class="highlight-green">data</span> "<span class="highlight-red">aws_ami</span>" "<span class="highlight-blue">amazon_linux</span>" {
  most_recent = true
  owners      = ["amazon"]

  filter {
    name   = "name"
    values = ["amzn2-ami-hvm-*"]
  }
}

resource "aws_instance" "web" {
  ami = <span class="highlight-green">data</span>.<span class="highlight-red">aws_ami</span>.<span class="highlight-blue">amazon_linux</span>.id
  instance_type = "t2.micro"
}
</pre>

---

## Variables

Rendre les configurations r√©utilisables.

```hcl
variable "instance_type" {
  description = "Type d'instance EC2"
  type        = string
  default     = "t2.micro"
}

variable "environment" {
  description = "Nom de l'environnement"
  type        = string
}
```

---

## Utilisation des variables

```hcl
resource "aws_instance" "web" {
  ami           = "ami-0c55b159cbfafe1f0"
  instance_type = var.instance_type

  tags = {
    Name        = "web-${var.environment}"
    Environment = var.environment
  }
}
```

---

## Passer des variables

**Ligne de commande :**
```bash
terraform apply \
  -var="instance_type=t2.small" \
  -var="environment=prod"
```

**Fichier terraform.tfvars :**
```hcl
instance_type = "t2.small"
environment   = "prod"
```

---

## Outputs (Sorties)

Afficher des informations apr√®s l'ex√©cution.

```hcl
output "instance_ip" {
  description = "IP publique"
  value       = aws_instance.web.public_ip
}

output "instance_id" {
  value = aws_instance.web.id
}
```

## Utilisation des outputs

```bash
$ terraform output
instance_ip = "54.123.45.67"
instance_id = "i-0abcd1234efgh5678"
```

---

## State (√âtat)

Le fichier `terraform.tfstate` est la base de donn√©es de Terraform :
- IDs des ressources
- Configuration actuelle
- M√©tadonn√©es et d√©pendances

Terraform :
- Le tient √† jour
- Sait exactement ce qui existe
- Compare facilement le code et le _state_ : planification rapide

‚õî Ne pas modifier l'infrastructure par un autre moyen

‚õî Ne pas modifier le _state_ soi-m√™me

---

## Emplacements du State

- **Local** : Sur votre machine (d√©faut)
- **Distant** : S3, Terraform Cloud (recommand√©)

```hcl
terraform {
  backend "s3" {
    bucket = "my-terraform-state"
    key    = "prod/terraform.tfstate"
    region = "us-east-1"
  }
}
```
‚ö†Ô∏è il faut cr√©er ce bucket **avant** d'utiliser terraform ‚ö†Ô∏è

---

## S√©curit√© du State

‚ö†Ô∏è **Consid√©rations importantes :**
- Contient des donn√©es sensibles - s√©curisez-le !
- Ne jamais l'√©diter manuellement
- Utiliser l'√©tat distant pour la collaboration
- Activer le verrouillage d'√©tat

<!--
sur AWS le state locking se fait avec une base dynamoDB,
sur Azure le blob storage le g√®re tout seul, idem chez GCP
-->

---

## Idempotence

Ex√©cuter plusieurs fois `terraform apply` ‚Üí m√™me r√©sultat final

```bash
$ terraform apply  # Cr√©e 1 instance
$ terraform apply  # Aucun changement
$ terraform apply  # Aucun changement
```

**Comment √ßa marche :**
1. Lire l'√©tat souhait√© (`.tf`)
2. Lire l'√©tat actuel (`.tfstate`)
3. Appliquer uniquement les diff√©rences

---

# TP

---

## Un point sur l'organisation des fichiers

- Seule l'extension est vraiment importante (`.tf`)
- Il n'y a pas d'ordre √† respecter (puisque c'est **d√©claratif**)
- Terraform lit tous les fichiers du r√©pertoire courant, retrouve les providers d√©clar√©s, les d√©finitions de variables, les `locals`, les ressources, les outputs, et construit un arbre de d√©pendances pour savoir dans quel ordre il faut proc√©der
- **Conclusion:** on peut tout √©crire dans 1 seul fichier `.tf`, **mais** on pr√©f√©rera r√©partir les √©l√©ments dans des fichiers avec des noms qui ont du sens

---

## Les trois √©tats dans Terraform

![Les trois √©tats dans Terraform](./images/trois-etats-terraform.svg)

- **√âtat souhait√©** : Ce que vous √©crivez dans vos fichiers `.tf`
- **√âtat connu** : Ce que Terraform a enregistr√© dans `.tfstate`
- **√âtat r√©el** : Ce qui existe r√©ellement dans l'infrastructure

---

# 6. Flux de travail Terraform

---

## Commandes de base

```
terraform init      # Initialiser
terraform fmt       # Formater
terraform validate  # Valider
terraform plan      # Voir changements
terraform apply     # Appliquer
terraform output    # Voir outputs
terraform destroy   # D√©truire
```

---

## Workflow Terraform

```
Code Terraform (.tf)
       ‚Üì
terraform init
       ‚Üì
terraform plan
       ‚Üì
terraform apply
       ‚Üì
Ressources r√©elles
```

---

## terraform init
- Initialise un r√©pertoire de travail, installe les providers et modules.
- Premi√®re fois dans un projet
- Apr√®s l'ajout de providers/modules

---

## terraform plan

Cr√©e un plan d'ex√©cution.

**Symboles :**
- `+` : Ressource cr√©√©e
- `-` : Ressource d√©truite
- `~` : Ressource modifi√©e
- `-/+` : Ressource remplac√©e

---

## Exemple de sortie de plan

```
Terraform will perform the following actions:

  # aws_instance.web will be created
  + resource "aws_instance" "web" {
      + ami           = "ami-0c55b159cbfafe1f0"
      + instance_type = "t2.micro"
      + id            = (known after apply)
    }

Plan: 1 to add, 0 to change, 0 to destroy.
```

---

### terraform apply

Applique les changements.

```bash
# Auto-approuver
terraform apply -auto-approve

# Avec variables
terraform apply -var="instance_type=t2.small"
```

### terraform destroy

D√©truit toutes les ressources g√©r√©es.

```bash
# D√©truire une ressource sp√©cifique
terraform destroy -target=aws_instance.web
```

---

## Commandes utilitaires

### terraform fmt

Formater le code.

```bash
terraform fmt              # Formater
terraform fmt -check       # V√©rifier
terraform fmt -recursive   # R√©cursif
```

### terraform validate

Valider la syntaxe.


---

## Commandes d'√©tat

```bash
# Lister les ressources
terraform state list

# Afficher une ressource
terraform state show aws_instance.web

# Retirer de l'√©tat
terraform state rm aws_instance.web

# Renommer
terraform state mv aws_instance.old aws_instance.new
```

<!--
le state mv sert quand on renomme une ressource dans le code tf
-->

---

## Autres commandes utiles

```bash
# Afficher l'√©tat actuel
terraform show

# Sync avec l'infra r√©elle
terraform refresh

# Cr√©er un graphe
terraform graph | dot -Tpng > graph.png
```

---

# 7. Concepts avanc√©s

  Modules, locals, count, for_each, ...

---

## Modules

Conteneurs pour plusieurs ressources utilis√©es ensemble.

**Structure :**
```
modules/
  ‚îî‚îÄ‚îÄ vpc/
      ‚îú‚îÄ‚îÄ main.tf
      ‚îú‚îÄ‚îÄ variables.tf
      ‚îî‚îÄ‚îÄ outputs.tf
```

---

## D√©finition de module

```hcl
resource "aws_vpc" "main" {
  cidr_block = var.vpc_cidr

  tags = {
    Name = var.vpc_name
  }
}

resource "aws_subnet" "public" {
  vpc_id     = aws_vpc.main.id
  cidr_block = var.public_subnet_cidr
}
```

---

## Utilisation du module

```hcl
module "vpc" {
  source = "./modules/vpc"

  vpc_name           = "production-vpc"
  vpc_cidr           = "10.0.0.0/16"
  public_subnet_cidr = "10.0.1.0/24"
}

resource "aws_instance" "web" {
  subnet_id = module.vpc.public_subnet_id
}
```

---

## Avantages des modules

- ‚úÖ R√©utilisabilit√© du code
- ‚úÖ Organisation
- ‚úÖ Encapsulation
- ‚úÖ Contr√¥le de version des composants

---

## Locals

Valeurs locales pour la r√©utilisation.

```hcl
locals {
  common_tags = {
    Environment = "Production"
    ManagedBy   = "Terraform"
  }
  instance_name = "${var.project}-${var.env}-web"
}

resource "aws_instance" "web" {
  tags = merge(local.common_tags, {
    Name = local.instance_name
  })
}
```

---

## Variables vs Locals : Quelle diff√©rence ?

|  | **Variables** (`var.*`) | **Locals** (`local.*`) |
|--------|-------------------------|------------------------|
| **Nature** | Entr√©es configurables | Valeurs calcul√©es internes |
| **Source** | Fournies par l'utilisateur | D√©riv√©es de variables/ressources |
| **Modification** | Peut changer entre ex√©cutions | Calcul√©e √† chaque ex√©cution |
| **Usage** | Param√®tres d'entr√©e | √âviter la r√©p√©tition de calculs |


- Utilisez **variables** pour ce qui vient de l'ext√©rieur
- Utilisez **locals** pour ce qui est calcul√©/transform√© en interne

---

<style scoped>
.columns {
  display: grid;
  grid-template-columns: 1fr 1fr;
  gap: 2rem;
}
</style>

## Exemple : Variables vs Locals

<div class="columns">
<div>

```hcl
# Variables : entr√©es utilisateur
variable "environment" {
  type = string
}

variable "project" {
  type = string
}

# Locals : valeurs calcul√©es
locals {
  # √âvite de r√©p√©ter cette construction partout
  name_prefix = "${var.project}-${var.environment}"

  # Tags communs d√©riv√©s des variables
  common_tags = {
    Project     = var.project
    Environment = var.environment
    ManagedBy   = "Terraform"
    CreatedAt   = timestamp()
  }
}
```
</div>
<div>

```hcl
# Utilisation
resource "aws_s3_bucket" "data" {
  bucket = "${local.name_prefix}-data"
  tags   = local.common_tags
}

resource "aws_s3_bucket" "logs" {
  bucket = "${local.name_prefix}-logs"
  tags   = local.common_tags
}
```
</div>
</div>

---

## Count

```hcl
resource "aws_instance" "server" {
  count         = 3
  ami           = var.ami_id
  instance_type = "t2.micro"

  tags = {
    Name = "server-${count.index}"
  }
}
```

---

## for_each

```hcl
variable "instances" {
  default = {
    web = { instance_type = "t2.micro" }
    api = { instance_type = "t2.small" }
  }
}

resource "aws_instance" "server" {
  for_each      = var.instances
  ami           = var.ami_id
  instance_type = each.value.instance_type

  tags = { Name = each.key }
}
```

---

## Expressions conditionnelles

```hcl
resource "aws_instance" "web" {
  ami = var.ami_id
  instance_type = var.environment == "production" ? \
                  "t2.large" : "t2.micro"

  tags = {
    Name = var.environment == "production" ? \
           "prod-web" : "dev-web"
  }
}
```

---

## Blocs dynamiques : Variable

```hcl
variable "ingress_rules" {
  type = list(object({
    port        = number
    cidr_blocks = list(string)
  }))
  default = [
    { port = 80,  cidr_blocks = ["0.0.0.0/0"] },
    { port = 443, cidr_blocks = ["0.0.0.0/0"] }
  ]
}
```

---

## Blocs dynamiques : Utilisation

```hcl
resource "aws_security_group" "web" {
  name = "web-sg"

  dynamic "ingress" {
    for_each = var.ingress_rules
    content {
      from_port   = ingress.value.port
      to_port     = ingress.value.port
      protocol    = "tcp"
      cidr_blocks = ingress.value.cidr_blocks
    }
  }
}
```

---

## Workspaces : Commandes

G√©rer plusieurs environnements.

```bash
# Lister
terraform workspace list

# Cr√©er et basculer
terraform workspace new dev
terraform workspace new prod

# Basculer
terraform workspace select dev
```

---

## Workspaces : Utilisation

```hcl
resource "aws_instance" "web" {
  ami = var.ami_id
  instance_type = terraform.workspace == "prod" ? \
                  "t2.large" : "t2.micro"

  tags = {
    Name        = "web-${terraform.workspace}"
    Environment = terraform.workspace
  }
}
```

---

## Remote State Data Source

Partager des donn√©es entre projets Terraform distincts mais d√©pendants.

**Cas d'usage :**
- Projet A : infrastructure r√©seau (VPC, subnets)
- Projets B, C, D : applications qui utilisent ce r√©seau

---

## Exemple : Remote State avec S3

**Projet A** (stocke son state dans S3)

```hcl
# Configuration du backend S3 pour stocker le state
terraform {
  backend "s3" {
    bucket = "mon-bucket-terraform-state"
    key    = "network/terraform.tfstate"
    region = "eu-west-3"
  }
}

# Cr√©ation du VPC
resource "aws_vpc" "main" { cidr_block = "10.0.0.0/16" ; tags = { Name = "vpc-principal" } }

resource "aws_subnet" "public" {
  vpc_id = aws_vpc.main.id ; cidr_block = "10.0.1.0/24" ; tags = { Name = "subnet-public" }
}

# Exporter les valeurs pour les autres projets
output "vpc_id" { value = aws_vpc.main.id }

output "subnet_id" { value = aws_subnet.public.id }
```

---

**Projet B** (lit le state de A)

```hcl
# Lire le state du projet A depuis S3
data "terraform_remote_state" "network" {
  backend = "s3"
  config = {
    bucket = "mon-bucket-terraform-state"
    key    = "network/terraform.tfstate"
    region = "eu-west-3"
  }
}

# Utiliser les outputs du projet A
resource "aws_instance" "web" {
  ami           = "ami-12345678"
  instance_type = "t2.micro"

  # Utilisation du subnet_id export√© par le projet A
  subnet_id = data.terraform_remote_state.network.outputs.subnet_id

  tags = {
    Name = "serveur-web"
    VPC  = data.terraform_remote_state.network.outputs.vpc_id
  }
}

```

---

# 8. Outils alternatifs

---

## OpenTofu

Fork open-source de Terraform cr√©√© suite au changement de licence de Terraform.

**Points cl√©s :**
- 100% compatible avec Terraform
- M√™me syntaxe HCL
- G√©r√© par la communaut√©
- Vraiment open source

üîç En fait le changement de licence impacte peu de monde et ne concerne que les entreprises qui vendent une solution d'infrastructure ou de gestion de cloud int√©grant Terraform. Quelques soci√©t√©s concern√©es : Pulumi, Spacelift, Scalr, Aiven

---

## Ressources

- **Documentation** : terraform.io/docs
- **Terraform Registry** : registry.terraform.io
- **Provider AWS** : registry.terraform.io/providers/hashicorp/aws
- **HashiCorp Learn** : learn.hashicorp.com
- **Communaut√©** : discuss.hashicorp.com

